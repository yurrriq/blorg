#+STARTUP: showall
#+OPTIONS: toc:nil ^:{}
#+HTML_MATHJAX: align: left indent: 5em tagside: left font: Neo-Euler
#+BEGIN_EXPORT html
---
title:  "Proving Addition is Commutative in Idris"
author: Eric Bailey
tags: idris, theorem-proving, books
---
#+END_EXPORT

First off, I think [[http://www.idris-lang.org][Idris]] is pretty great. Over the past several months, I've
been thoroughly enjoying reading and working through [[https://www.manning.com/books/type-driven-development-with-idris][/Type-Driven Development
with Idris/]], as well as chatting with the community on IRC (=#idris= on
freenode).

For every [[https://www.manning.com/meap-program][MEAP]] book, Manning provides a forum. To be honest, though I've
partaken in quite a few MEAPs, I've never really participated in the fora. In
[[https://forums.manning.com/forums/type-driven-development-with-idris][the /Type-Driven Development with Idris/ forum]], however, I've made a whopping
*two* posts so far. One was a clarifying question about a possible error in the
text and the other was an answer to a fellow reader's question. My answer seemed
like a good example of how Idris works, so I thought I'd turn it into a blog
post. Here goes.

In this example, we're trying to prove that addition of natural numbers is
[[https://en.wikipedia.org/wiki/Commutative_property][commutative]], i.e.
$$\forall n,m \in \mathbb{N}\ (n + m \equiv m + n)$$
... which we can express in Idris as follows:
#+BEGIN_SRC idris
myPlusCommutative : (n, m : Nat) -> n + m = m + n
#+END_SRC

The type signature for ~myPlusCommutative~ can be read as something like, "For
all natural numbers =n= and =m=, =n= plus =m= is exactly equivalent to =m= plus
=n=."

Over-simplifying the [[https://en.wikipedia.org/wiki/Curry%25E2%2580%2593Howard_correspondence][Curry-Howard correspondence]], if we can write a [[https://en.wikipedia.org/wiki/Total_functional_programming][total
function]] that satisfies the type signature, then we've effectively written a
proof for the corresponding [[https://en.wikipedia.org/wiki/Universal_quantification][universal quantification]].

Idris has some great interactive editing tools, so I'll try to show those off a
bit here too. For a start, we can call ~idris-add-clause~ (=M-RET d= in
[[http://spacemacs.org/layers/+lang/idris/README.html][Spacemacs]]) to get:
#+BEGIN_SRC idris
myPlusCommutative : (n, m : Nat) -> n + m = m + n
myPlusCommutative n m = ?myPlusCommutative_rhs
#+END_SRC

The prefix =?= signifies a hole and in this case, the type of that hole is:
#+BEGIN_SRC idris
  n : Nat
  m : Nat
--------------------------------------
myPlusCommutative_rhs : plus n m = plus m n
#+END_SRC

Next, we can case split (~idris-case-split~, =M-RET c=) on ~n~:
#+BEGIN_SRC idris
myPlusCommutative : (n, m : Nat) -> n + m = m + n
myPlusCommutative  Z    m = ?myPlusCommutative_rhs_1
myPlusCommutative (S k) m = ?myPlusCommutative_rhs_2
#+END_SRC

For ~?myPlusCommutative_rhs_1~ we need to prove that $m = m + 0$.
#+BEGIN_SRC idris
myPlusCommutative_rhs_1 : m = plus m 0
#+END_SRC

Fortunately, the =Prelude= has a proof that's almost exactly what we want:
#+BEGIN_SRC idris
plusZeroRightNeutral : (left : Nat) -> left + 0 = left
#+END_SRC

Since we need to swap the left- and right-hand sides, we can use ~sym~:
#+BEGIN_SRC idris
sym : (left = right) -> right = left
#+END_SRC

So the complete definition for the [[https://en.wikipedia.org/wiki/Mathematical_induction][/base case/]] of ~myPlusCommutative~ is:
#+BEGIN_SRC idris
myPlusCommutative  Z    m = sym (plusZeroRightNeutral m)
#+END_SRC

Next, we've got to tackle the /inductive step/. Since we know we'll need to
reduce to the /base case/, let's define a local variable ~inductiveHypothesis~:
#+BEGIN_SRC idris
myPlusCommutative (S k) m =
  let inductiveHypothesis = myPlusCommutative k m in
    rewrite inductiveHypothesis in
    ?myPlusCommutative_rhs_2
#+END_SRC

Examining the type of ~?myPlusCommutative_rhs_2~:
#+BEGIN_SRC idris
  k : Nat
  m : Nat
  inductiveHypothesis : plus k m = plus m k
  _rewrite_rule : plus k m = plus m k
--------------------------------------
myPlusCommutative_rhs_2 : S (plus m k) = plus m (S k)
#+END_SRC

... we see we need to prove the successor of $k$ plus $m$ is exactly equivalent
to $m$ plus the successor of $k$, i.e.
$$\forall m \in \mathbb{N}\ (S(k + m) \equiv m + S(k))$$

Prelude to the rescue, yet again:
#+BEGIN_SRC idris
plusSuccRightSucc : (left : Nat) ->
                    (right : Nat) -> S (left + right) = left + S right
#+END_SRC

Now we can ~rewrite~ the hole using ~plusSuccRightSucc~ with ~m~ as ~left~ and
~k~ as ~right~:
#+BEGIN_SRC idris
myPlusCommutative (S k) m =
  let inductiveHypothesis = myPlusCommutative k m in
    rewrite inductiveHypothesis in
    rewrite plusSuccRightSucc m k in
            ?myPlusCommutative_rhs_2
#+END_SRC

Eliding the repetitive bits, we get:
#+BEGIN_SRC idris
myPlusCommutative_rhs_2 : plus m (S k) = plus m (S k)
#+END_SRC

... which looks like our old friend, ~Refl~:
#+BEGIN_SRC idris
Refl : x = x
#+END_SRC

At this point, Idris can fill in the hole for us, via ~idris-proof-search~
(=M-RET p=):

#+BEGIN_SRC idris
myPlusCommutative (S k) m =
  let inductiveHypothesis = myPlusCommutative k m in
    rewrite inductiveHypothesis in
    rewrite plusSuccRightSucc m k in
            Refl
#+END_SRC

Tidying up a bit for my taste, we arrive at the complete (and total) definition:
#+BEGIN_SRC idris
||| Addition of natural numbers is commutative.
total
myPlusCommutative : (n, m : Nat) -> n + m = m + n
myPlusCommutative  Z    m = sym (plusZeroRightNeutral m)
myPlusCommutative (S k) m = rewrite myPlusCommutative k m in
                                    plusSuccRightSucc m k
#+END_SRC
$\square$
